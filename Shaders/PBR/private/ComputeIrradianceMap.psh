// Generates an irradiance cube from an environment map using convolution

#include "PBR_PrecomputeCommon.fxh"
#include "ShaderUtilities.fxh"
#include "PBR_Common.fxh"

#ifndef OPTIMIZE_SAMPLES
#   define OPTIMIZE_SAMPLES 1
#endif

#if ENV_MAP_TYPE == ENV_MAP_TYPE_CUBE
TextureCube  g_EnvironmentMap;
#elif ENV_MAP_TYPE == ENV_MAP_TYPE_SPHERE
Texture2D    g_EnvironmentMap;
#endif

SamplerState g_EnvironmentMap_sampler;

cbuffer FilterAttribs
{
    float4x4 g_RotationUnused;

    float g_Roughness;
    float g_EnvMapWidth;
    float g_EnvMapHeight;
    float g_EnvMipCount;
    
    uint  g_NumSamples;
    uint  _Padding0;
    uint  _Padding1;
    uint  _Padding2;
}

float3 SampleEnvrionmentMap(float3 R, float MipLevel)
{
#if ENV_MAP_TYPE == ENV_MAP_TYPE_CUBE
    return g_EnvironmentMap.SampleLevel(g_EnvironmentMap_sampler, R, MipLevel).rgb;
#elif ENV_MAP_TYPE == ENV_MAP_TYPE_SPHERE
    return g_EnvironmentMap.SampleLevel(g_EnvironmentMap_sampler, TransformDirectionToSphereMapUV(R), MipLevel).rgb;
#endif
}

float3 IrradianceMap(float3 N)
{
    float3 T, B;
    BasisFromNormal(N, T, B);
    
    // When the HDRI map has points occupying a small area but with high peak brightness (such as the sun), brightness spikes appear on the irradiance map. 
    // To resolve this issue, we used the approach from the comments in this post:
    // https://www.reddit.com/r/opengl/comments/l0h5fy/rendering_the_irradiance_map_for_physically_based/
    float3 Irradiance = float3(0.0, 0.0, 0.0);   
    for (uint i = 0u; i < g_NumSamples; ++i)
    {
        float2 Xi = Hammersley2D(i, g_NumSamples);
        
        // Importance sample the hemisphere with a cosine-weighted distribution
        float3 L;
        float  pdf;
        SampleDirectionCosineHemisphere(Xi, L, pdf);
        L = normalize(L.x * T + L.y * B + L.z * N);

#if OPTIMIZE_SAMPLES       
        // Solid angle of current smple
        float OmegaS = 1.0 / (float(g_NumSamples) * pdf);
        
#if ENV_MAP_TYPE == ENV_MAP_TYPE_CUBE
        // Solid angle of 1 pixel across all cube faces
        float OmegaP = ComputeCubeMapPixelSolidAngle(g_EnvMapWidth, g_EnvMapHeight);
#else
        // Solid angle of 1 pixel on sphere
        float OmegaP = ComputeSphereMapPixelSolidAngle(g_EnvMapWidth, g_EnvMapHeight, acos(L.y), 0.5);
#endif
        // Applying mip bias produces better results, especially for environment maps with
        // very bright spots.
        float MipBias = 1.0;
        float MipLevel = clamp(0.5 * log2(OmegaS / max(OmegaP, 1e-10)) + MipBias, 0.0, g_EnvMipCount - 1.0);
#else
        float MipLevel = 0.0;
#endif
        Irradiance += SampleEnvrionmentMap(L, MipLevel);
    }
    return Irradiance / float(g_NumSamples);
}

void main(in float4 Pos      : SV_Position,
          in float3 WorldPos : WORLD_POS,
          out float4 Color   : SV_Target)
{
    float3 N = normalize(WorldPos);
    Color.rgb = IrradianceMap(N);
    Color.a = 1.0;
}
